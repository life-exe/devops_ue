def ColorFromBuildResult(result){
    if("${result}" == 'SUCCESS') return '#18A558'
    if("${result}" == 'ABORTED') return '#6e6e6e'
    if("${result}" == 'UNSTABLE') return '#fcba03'
    return '#FF0000'
}

pipeline {
    agent {
        node {
            label "unreal_test_runner_label"
            customWorkspace "$BUILD_DIR"
        }
    }

    environment {/*
        BUILD_DIR = 'c:\\JenkinsJobs\\TPS-test-runner-pipeline'
        GIT_URL_GAME = 'https://github.com/life-exe/UnrealTPSGame'
        GIT_CREDENTIALS_GAME = 'life-exe-github'
        GIT_BRANCH_GAME = 'master'
        GIT_CLONE_TIMEOUT_MIN = 60
        UAT_PATH = 'C:\\Epic Games\\UE Binary\\UE_5.0\\Engine\\Build\\BatchFiles\\RunUAT.bat'
        PROJECT_PATH = "$BUILD_DIR\\TPS.uproject"
        BUILD_FLAGS = '-build -cook -ubtargs="-UnoptimizedCode"'
        SLACK_CHANNEL = 'tps-game-builds'
        PLATFORM = 'Win64' // ['Android', 'Linux'..]
        CONFIG = 'Development' // ['Development', 'Shipping', 'DebugGame']
        EDITOR_PATH = 'c:\\Epic Games\\UE Binary\\UE_5.0\\Engine\\Binaries\\Win64\\UnrealEditor.exe'
        TEST_NAMES = 'TPSGame'
        TEST_OUTPUT_PATH = "$BUILD_DIR\\Build\\Tests"
        EDITOR_FLAGS = '-stdout -nosplash'
        CODE_COVERAGE_EXPORT_TYPE = "cobertura:$BUILD_DIR\\Build\\Tests\\Coverage.xml"
        OPEN_CPP_COVERAGE_PATH = 'C:\\Program Files\\OpenCppCoverage\\OpenCppCoverage.exe'
        SOURCE_CODE_PATH = "$BUILD_DIR\\Source"
        EXCLUDED_SOURCES = "$BUILD_DIR\\Source\\TPS\\Tests"
        PROJECT_PURE_NAME = 'TPS'
        UNOPTIMIZED_BUILD = 'false'
        TEST_EXTRA_CMD = 'SetPriority Critical;'
        */

        UNOPTIMIZE_FALSE_LINE = 'public bool UnoptimizedCode = false;'
        UNOPTIMIZE_TRUE_LINE = 'public bool UnoptimizedCode = true;'
        EDITOR_TARGET_PATH = "$SOURCE_CODE_PATH\\${PROJECT_PURE_NAME}Editor.Target.cs"
    }

    stages {
        stage('Git clone') {
            steps {
                slackSend channel: "$SLACK_CHANNEL",
                    color: "$currentBuild.previousBuild" == 'null' ? '#18A558' :
                    ColorFromBuildResult("$currentBuild.previousBuild.result"),
                    message:"Build started: ${env.JOB_NAME} #${env.BUILD_NUMBER} (<${env.BUILD_URL}|Open>)"

                checkout([$class: 'GitSCM',
                    branches: [[name: "$GIT_BRANCH_GAME"]],
                    extensions: [
                        [$class: 'CleanBeforeCheckout'],
                        [$class: 'SubmoduleOption', disableSubmodules: false, recursiveSubmodules: true],
                        [$class: 'CloneOption', timeout: "$GIT_CLONE_TIMEOUT_MIN"],
                        [$class: 'CheckoutOption', timeout: "$GIT_CLONE_TIMEOUT_MIN"]
                    ],
                    userRemoteConfigs: [[credentialsId: "$GIT_CREDENTIALS_GAME",
                    url: "$GIT_URL_GAME"]]])
            }
        }
        stage('Set unoptimized build') {
            when {
                environment name: 'UNOPTIMIZED_BUILD', value: 'true'
            }
            steps {
                bat '''
                    powershell -Command "(gc '%EDITOR_TARGET_PATH%') -replace '%UNOPTIMIZE_FALSE_LINE%', '%UNOPTIMIZE_TRUE_LINE%' | Out-File  '%EDITOR_TARGET_PATH%'"
                '''
            }
        }
        stage('Build') {
            steps {
                bat '''
                    "%UAT_PATH%" BuildCookRun ^
                    -project="%PROJECT_PATH%" ^
                    -platform="%PLATFORM%" ^
                    -clientconfig="%CONFIG%" ^
                    %BUILD_FLAGS% -noturnkeyvariables
                '''
            }
        }
        stage('Test') {
            steps {
                bat '''
                    set TEST_RUNNER="%EDITOR_PATH%" "%PROJECT_PATH%" -ExecCmds="Automation %TEST_EXTRA_CMD%RunTests %TEST_NAMES%;Quit" ^
                    -ReportExportPath="%TEST_OUTPUT_PATH%" %EDITOR_FLAGS%

                    "%OPEN_CPP_COVERAGE_PATH%" --modules="%BUILD_DIR%" --sources="%SOURCE_CODE_PATH%" ^
                    --excluded_sources="%EXCLUDED_SOURCES%" --export_type="%CODE_COVERAGE_EXPORT_TYPE%" -- %TEST_RUNNER%

                    if errorlevel 255 echo WARNING: Some tests failed, error code: %errorlevel% & exit /b 0
                '''
            }
        }
        stage('Unset unoptimized build') {
            when {
                environment name: 'UNOPTIMIZED_BUILD', value: 'true'
            }
            steps {
                bat '''
                    powershell -Command "(gc '%EDITOR_TARGET_PATH%') -replace '%UNOPTIMIZE_TRUE_LINE%', '%UNOPTIMIZE_FALSE_LINE%' | Out-File  '%EDITOR_TARGET_PATH%'"
                '''
            }
        }
        stage('Publish test results') {
            environment {
                CONVERTER_PATH="$BUILD_DIR\\devops_ue\\tests\\ue_report_to_junit.py"
                UE_TESTS_PATH="$BUILD_DIR\\Build\\Tests\\index.json"
                JUNIT_TESTS_PATH="$BUILD_DIR\\Build\\Tests\\index.xml"
                COBERTURA_PATCHER_PATH="$BUILD_DIR\\devops_ue\\tests\\cobertura_report_patcher.py"
                CODE_COVERAGE_PATH="$BUILD_DIR\\Build\\Tests\\Coverage.xml"
            }
            steps {
                bat '''
                    "%CONVERTER_PATH%" -ue_report_path="%UE_TESTS_PATH%" -junit_xml_path="%JUNIT_TESTS_PATH%"
                    "%COBERTURA_PATCHER_PATH%" -cobertura_report_path="%CODE_COVERAGE_PATH%"
                '''
                junit 'Build\\Tests\\index.xml'
                cobertura coberturaReportFile: 'Build\\Tests\\Coverage.xml', onlyStable: false
            }
        }
    }

    post {
        always {
            slackSend channel: "$SLACK_CHANNEL",
                color: ColorFromBuildResult("$currentBuild.currentResult"),
                message:"Build $currentBuild.currentResult: ${env.JOB_NAME} #${env.BUILD_NUMBER} (<${env.BUILD_URL}|Open>)"
        }
    }
}
